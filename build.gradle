plugins {
    id 'java'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
}

scmVersion {
    tag {
        prefix = project.name
    }
    createReleaseCommit true
    releaseCommitMessage { version, position -> "release version: $version"}
}

group = 'com.tealcubegames.minecraft.spigot'
version = scmVersion.version

apply from: 'publish-java.gradle'

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

dependencies {
    compile('org.spigotmc:spigot-api:1.9-SNAPSHOT') {
        exclude module: 'bungeecord-chat'
    }
    compile 'org.apache.commons:commons-lang3:3.3.2'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    ext {
        name = project.name
        year = Calendar.getInstance().get(Calendar.YEAR)
        author = 'Teal Cube Games'
        url = 'http://tealcubegames.com/'
    }
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
