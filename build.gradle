plugins {
    id "java"
    id "maven-publish"
    id "com.github.hierynomus.license" version "0.11.0"
    id "signing"
    id "pl.allegro.tech.build.axion-release" version "1.2.4"
}

sourceCompatibility = 1.7

scmVersion {
    tag {
        prefix = project.name
    }
    createReleaseCommit true
    releaseCommitMessage { version, position -> "[ci skip] release version: " + version}
}

group = "com.tealcube.minecraft.bukkit"
version = scmVersion.version

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

dependencies {
    compile "org.bukkit:bukkit:1.8-R0.1-SNAPSHOT"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    if (!project.version.endsWith("SNAPSHOT")) {
        sign configurations.archives
    }
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                def rootNode = asNode()
                rootNode.appendNode("name", project.name)
                rootNode.appendNode("description", project.name + " " + project.version)
                rootNode.appendNode("inceptionYear", "2015")

                def orgNode = rootNode.appendNode("organization")
                orgNode.appendNode("name", "Teal Cube Games")
                orgNode.appendNode("url", "http://tealcube.com/")

                def scmNode = rootNode.appendNode("scm")
                scmNode.appendNode("connection", "scm:git:https://github.com/TealCube/" + project.name + ".git")
                scmNode.appendNode("developerConnection", "scm:git:https://github.com/TealCube/" + project.name + ".git")
                scmNode.appendNode("url", "https://github.com/TealCube/" + project.name)
                scmNode.appendNode("tag", "HEAD")
            }
        }
    }
    repositories {
        maven {
            url getMavenPublishingRepo()
            name "MavenCentral"
            credentials {
                username System.getenv("OSSRH_USERNAME") ?: OSSRH_USERNAME
                password System.getenv("OSSRH_PASSWORD") ?: OSSRH_PASSWORD
            }
        }
    }
}

def getMavenPublishingRepo() {
    if (!project.version.endsWith('-SNAPSHOT')) {
        return "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }
    return "https://oss.sonatype.org/content/repositories/snapshots/"
}